plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

apply from: '../dependencies.gradle'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    def globalConfiguration = project.extensions.getByName("ext")

    buildToolsVersion globalConfiguration["buildToolsVersion"]
    compileSdkVersion globalConfiguration["targetSdkVersion"]

    buildFeatures {
        viewBinding true
    }

    defaultConfig {
        applicationId globalConfiguration["applicationId"]
        minSdkVersion globalConfiguration["minSdkVersion"]
        targetSdkVersion globalConfiguration["targetSdkVersion"]
        versionCode globalConfiguration["versionCode"]
        versionName globalConfiguration["versionName"]
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {

        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    sourceSets {

        betaDebug {
            res.srcDirs += ['src/beta/res']
        }
        betaRelease {
            res.srcDirs += ['src/beta/res']
        }
        storeDebug {
            res.srcDirs += ['src/store/res']
        }
        storeRelease {
            res.srcDirs += ['src/store/res']
        }

    }

    buildTypes {

        debug {
            debuggable true
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
        }

        release {
            shrinkResources true
            minifyEnabled true
            debuggable false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"

    productFlavors {

        beta {
            applicationIdSuffix ".beta"
            resValue "string", "appName", "Init App Beta"

        }

        store {
            resValue "string", "appName", "Init App"
        }
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation androidxDependencies.values()
    implementation architectureComponentsDependencies.values()
    implementation kotlinDependencies.values()
    implementation googleDependencies.values()
    implementation hiltDependencies.values()
    kapt hiltKaptDependencies.values()
    implementation networkingDependencies.values()
    implementation uiDependencies.values()
    annotationProcessor uiAnnotationDependencies.values()

    testImplementation testDependencies.values()
    androidTestImplementation androidTestDependencies.values()
    kaptAndroidTest kaptTestDependencies.values()
    androidTestImplementation("com.schibsted.spain:barista:$barista_version") {
        exclude group: 'org.jetbrains.kotlin'
    }

    implementation project(':login')
    implementation project(':core')
}
